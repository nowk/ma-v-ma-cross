{------------------------------------------------------------------------------
  Open Source under the MIT License – free to use, modify, and share.
  TradeStation® is a trademark of TradeStation Group, Inc.; all rights reserved.
------------------------------------------------------------------------------}

// Grabbing the initial setup from TS's own Mov Avg 2 Lines indicator

inputs:
	FastPrice( Close ) [
		DisplayName = "FastPrice", 
		ToolTip = "Enter an EasyLanguage expression to use in calculation of shorter length moving average."],
	
	SlowPrice( Close ) [
		DisplayName = "SlowPrice", 
		ToolTip = "Enter an EasyLanguage expression to use in calculation of longer length moving average."],
	
	FastLength( 20 ) [
		DisplayName = "FastLength", 
		ToolTip = "Enter number of bars to use in calculation of shorter length moving average."],
	
	SlowLength( 50 ) [
		DisplayName = "SlowLength", 
		ToolTip = "Enter number of bars to use in calculation of longer length moving average."];
	
variables:
	FastAvg( 0 ),
	SlowAvg( 0 );

FastAvg = AverageFC( FastPrice, FastLength );
SlowAvg = AverageFC( SlowPrice, SlowLength );

// my stuff begins here

Inputs:
	int BullCrossIdx( 1 ),
	int BearCrossIdx( -1 ),
	int BullTrendIdx( 0 ),
	int BearTrendIdx( 0 );

Variables:
	bool isBullCross( false ),
	bool isBearCross( false ),
	int plotIndex( 0 );
	
// plot index is 0
plotIndex = 0;

// plot trend biases
If FastAvg > SlowAvg Then
Begin
	plotIndex = BullTrendIdx;
	Setplotcolor( 1, Green );
End
Else If FastAvg < SlowAvg Then
Begin
	plotIndex = BearTrendIdx;
	Setplotcolor( 1, Red );
End;

// TODO handle equal mas

// bull crossing
isBullCross = FastAvg[1] < SlowAvg[1] And FastAvg > SlowAvg;
If isBullCross = True Then
Begin
	plotIndex = BullCrossIdx;
	Setplotcolor( 1, Green );
End;

// bear crossing
isBearCross = FastAvg[1] > SlowAvg[1] And FastAvg < SlowAvg;
if isBearCross = True Then
Begin
	plotIndex = BearCrossIdx;
	Setplotcolor( 1, Red );
End;

Plot1(plotIndex, !("MaCrossIdx"));
