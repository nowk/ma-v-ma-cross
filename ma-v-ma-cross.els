{------------------------------------------------------------------------------
  Open Source under the MIT License – free to use, modify, and share.
  TradeStation® is a trademark of TradeStation Group, Inc.; all rights reserved.
------------------------------------------------------------------------------}

// Grabbing the initial setup from TS's own Mov Avg 2 Lines indicator

inputs:
	FastPrice( Close ) [
		DisplayName = "FastPrice", 
		ToolTip = "Enter an EasyLanguage expression to use in calculation of shorter length moving average."],
	
	SlowPrice( Close ) [
		DisplayName = "SlowPrice", 
		ToolTip = "Enter an EasyLanguage expression to use in calculation of longer length moving average."],
	
	FastLength( 20 ) [
		DisplayName = "FastLength", 
		ToolTip = "Enter number of bars to use in calculation of shorter length moving average."],
	
	SlowLength( 50 ) [
		DisplayName = "SlowLength", 
		ToolTip = "Enter number of bars to use in calculation of longer length moving average."];
	
variables:
	FastAvg( 0 ),
	SlowAvg( 0 );

FastAvg = AverageFC( FastPrice, FastLength );
SlowAvg = AverageFC( SlowPrice, SlowLength );

// my stuff begins here

Inputs:
	int BullCrossIdx( 1 ),
	int BearCrossIdx( -1 ),
	int BullTrendIdx( 0 ),
	int BearTrendIdx( 0 );

Variables:
	bool isBullCross( false ),
	bool isBearCross( false ),
	intrabarpersist bool TrendUp( false ),
	intrabarpersist bool TrendDn( false );

Setplottype( 2, Ptstring );
Setplottype( 3, Ptstring );

Begin
	Plot1( 0, !("MaCrossIdx"), White, Default, 2 );
	
	// specific to radar screen
	Plot2( "", "Trend" );
	Plot3( "", "Alert" );
	Setplotbgcolor( 3, Transparent );
End;

// plot trend biases
If FastAvg > SlowAvg Then
Begin
	Plot1(BullTrendIdx, !("MaCrossIdx"));
	Setplotcolor( 1, Green );
	
	If ( BarStatus( 1 ) = 2 ) then
	Begin
		TrendUp = true;
		TrendDn = false;
	End;
End;

If FastAvg < SlowAvg Then
Begin
	Plot1(BearTrendIdx, !("MaCrossIdx"));
	Setplotcolor( 1, Red );

	If ( BarStatus( 1 ) = 2 ) then
	Begin
		TrendUp = false;
		TrendDn = true;
	End;
End;

// NOTE for Radarscreen Trend coloring to work, need to load additional bars
// NOTE this must come after the assignments above, or it will not switch over
// after the close of the market.
If ( TrendUp ) Then
Begin
	Setplotbgcolor( 2, Green );
End;

If ( TrendDn ) Then
Begin
	Setplotbgcolor( 2, Red );
End;

// TODO handle equal mas

// bull crossing
isBullCross = FastAvg[1] < SlowAvg[1] And FastAvg > SlowAvg;
If isBullCross = True Then
Begin
	Plot1(BullCrossIdx, !("MaCrossIdx"));
	Setplotcolor( 1, Green );
	
	Plot3( !( "BullCross" ), "Alert" );
	Setplotbgcolor( 3, Blue );
	
	Alert( !("MaCrossIdx: BullCross") );
End;

// bear crossing
isBearCross = FastAvg[1] > SlowAvg[1] And FastAvg < SlowAvg;
if isBearCross = True Then
Begin
	Plot1(BearCrossIdx, !("MaCrossIdx"));
	Setplotcolor( 1, Red );
	
	Plot3( !( "BearCross" ), "Alert" );
	Setplotbgcolor( 3, Magenta );
	
	Alert( !("MaCrossIdx: BearCross") );
End;

